#include <SPIFFS.h>

#define setupSpiffsH true

#define WIFI_FILE "/wifi.json"
#define PARAMETRE_FILE "/parametres.json"

bool setupSPIFFS() {
  DEBUG_PRINT("SPIFFS.");
  
  // Vérifier rapidement si la partition SPIFFS existe
  esp_partition_iterator_t it = esp_partition_find(ESP_PARTITION_TYPE_DATA, ESP_PARTITION_SUBTYPE_DATA_SPIFFS, NULL);
  if (it == NULL) {
    DEBUG_PRINTLN(".PARTITION NOT FOUND - SPIFFS disabled in partition table");
    esp_partition_iterator_release(it);
    return false;  // Pas de partition SPIFFS, exit immédiatement
  }
  esp_partition_iterator_release(it);
  
  const int MAX_ATTEMPTS = 60;  // 60 tentatives
  const int DELAY_MS = 500;      // délai de 500ms entre chaque tentative
  
  int attempts = 0;
  
  while (!SPIFFS.begin(true) && attempts < MAX_ATTEMPTS) {
    DEBUG_PRINT(".");
    delay(DELAY_MS);
    attempts++;
  }
  
  if (attempts >= MAX_ATTEMPTS) {
    DEBUG_PRINTLN(".TIMEOUT - SPIFFS non disponible, continuation sans SPIFFS");
    return false;  // SPIFFS n'a pas pu être initialisé
  } else {
    DEBUG_PRINTLN(".Ok");
    return true;   // SPIFFS initialisé avec succès
  }
}
void setupSPIFFS_wififile() {
  const char* fileName = WIFI_FILE;

  // Vérifier si le fichier existe
  if (SPIFFS.exists(fileName)) {
    DEBUG_PRINTLN("wifi.json       : existe déjà");
    return;
  }

  // Créer un document JSON avec un réseau par défaut
  StaticJsonDocument<1024> doc;
  JsonArray wifiNetworks = doc.createNestedArray("wifi_networks");



  JsonObject defaultNetwork = wifiNetworks.createNestedObject();
  defaultNetwork["ssid"] = "0";
  defaultNetwork["mdp"] = "0";
  defaultNetwork["hostname"] = "0";
  defaultNetwork["IP_local"] = "0";
  defaultNetwork["MAC_ADRESS"] = "0";
  defaultNetwork["DNS"] = "0";
  defaultNetwork["mDNS"] = "0";
  defaultNetwork["port"] = "80";

  // Ouvrir le fichier pour l'écriture
  File file = SPIFFS.open(fileName, "w");
  if (!file) {
    DEBUG_PRINTLN("wifi.json       : Erreur d'ouverture du fichier pour écriture");
    return;
  }

  // Sérialiser le document JSON dans le fichier
  if (serializeJson(doc, file) == 0) {
    DEBUG_PRINTLN("wifi.json       : Erreur de sérialisation du JSON dans le fichier");
  } else {
    DEBUG_PRINTLN("wifi.json       : Créé avec succès");
  }

  // Fermer le fichier
  file.close();
}
void setupSPIFFS_parametres() {
    StaticJsonDocument<512> doc;
    bool modified = false;

    // Charger le fichier s'il existe
    if (SPIFFS.exists(PARAMETRE_FILE)) {
        File file = SPIFFS.open(PARAMETRE_FILE, "r");
        if (!file) {
            DEBUG_PRINTLN("parametres.json : Erreur d'ouverture du fichier pour lecture");
            return;
        }

        DeserializationError error = deserializeJson(doc, file);
        file.close();

        if (error) {
            DEBUG_PRINTLN("parametres.json : Erreur de désérialisation du JSON");
            return;
        }

        // Charger les valeurs existantes
        DEBUG_PRINTLN("parametres.json : Lecture du fichier qui existe déjà");
        param.fromJson(doc);
    } else {
        DEBUG_PRINTLN("parametres.json : Le fichier n'existe pas, il sera créé.");
        modified = true;
    }

    // Vérifier et ajouter les nouvelles variables si elles manquent
    #define X(type, name, defaultValue) \
        if (!doc.containsKey(#name)) { doc[#name] = param.name; modified = true; }
    PARAM_LIST
    #undef X

    // Sauvegarder uniquement si des modifications ont été faites
    if (modified) {
        File file = SPIFFS.open(PARAMETRE_FILE, "w");
        if (!file) {
            DEBUG_PRINTLN("parametres.json : Erreur d'ouverture du fichier pour écriture");
            return;
        }
        serializeJson(doc, file);
        file.close();
        DEBUG_PRINTLN("parametres.json : Fichier mis à jour avec les nouvelles variables.");
    }
}

void deleteFile(const char* fileName) {
  if(spiffs_ready == false) {
    DEBUG_PRINTLN("Erreur : SPIFFS n'est pas monté, impossible de supprimer le fichier.");
    return;
  }
  if (SPIFFS.begin(true)) { // Monte le système de fichiers, formatte s'il le faut
    if (SPIFFS.exists(fileName)) { // Vérifie si le fichier existe
      if (SPIFFS.remove(fileName)) {
        DEBUG_PRINTLN("Fichier supprimé avec succès !");
      } else {
        DEBUG_PRINTLN("Échec de la suppression du fichier.");
      }
    } else {
      DEBUG_PRINTLN("Fichier introuvable.");
    }
  } else {
    DEBUG_PRINTLN("Erreur : Impossible de monter le système de fichiers.");
  }
}


void printMemorySummary() {
    Serial.print("\n====== MEMOIRE ======\n");
    
    // === RAM ===
    uint32_t heap_used = ESP.getHeapSize() - ESP.getFreeHeap();
    uint32_t heap_free = ESP.getFreeHeap();
    uint32_t heap_total = ESP.getHeapSize();
    Serial.printf("RAM    : %.2f / %.2f Ko (%.2f Ko libre)\n", heap_used / 1024.0, heap_total / 1024.0, heap_free / 1024.0);
    
    // === PSRAM ===
    if (psramFound()) {
        uint32_t psram_used = ESP.getPsramSize() - ESP.getFreePsram();
        uint32_t psram_free = ESP.getFreePsram();
        uint32_t psram_total = ESP.getPsramSize();
        Serial.printf("PSRAM  : %.2f / %.2f Mo (%.2f Mo libre)\n", psram_used / (1024.0 * 1024), psram_total / (1024.0 * 1024), psram_free / (1024.0 * 1024));
    }

    // === FLASH Partitions ===
    const esp_partition_t* app0 = esp_partition_find_first(ESP_PARTITION_TYPE_APP, ESP_PARTITION_SUBTYPE_APP_OTA_0, NULL);
    const esp_partition_t* app1 = esp_partition_find_first(ESP_PARTITION_TYPE_APP, ESP_PARTITION_SUBTYPE_APP_OTA_1, NULL);
    const esp_partition_t* spiffs_partition = esp_partition_find_first(ESP_PARTITION_TYPE_DATA, ESP_PARTITION_SUBTYPE_DATA_SPIFFS, NULL);
    
    // Déterminer quelle app est actuellement active
    const char* active_app = "?";
    esp_partition_iterator_t it = esp_partition_find(ESP_PARTITION_TYPE_APP, ESP_PARTITION_SUBTYPE_ANY, NULL);
    if (it != NULL) {
        const esp_partition_t* partition = esp_partition_get(it);
        if (partition->subtype == ESP_PARTITION_SUBTYPE_APP_OTA_0) active_app = "App0";
        else if (partition->subtype == ESP_PARTITION_SUBTYPE_APP_OTA_1) active_app = "App1";
        esp_partition_iterator_release(it);
    }
    
    if (app0 && app1) {
        Serial.printf("OTA    : App0=%.2f Mo + App1=%.2f Mo (OUI - Dual OTA possible) [ACTIF: %s]\n", app0->size / (1024.0 * 1024), app1->size / (1024.0 * 1024), active_app);
    } else if (app0) {
        Serial.printf("OTA    : App0=%.2f Mo (NON - Single OTA)\n", app0->size / (1024.0 * 1024));
    } else {
        Serial.printf("OTA    : NON DISPONIBLE\n");
    }
    
    if (spiffs_partition) {
        if(spiffs_ready && SPIFFS.begin(true)) {
            Serial.printf("SPIFFS : %.2f / %.2f Mo (%.2f Mo libre)\n", 
                SPIFFS.usedBytes() / (1024.0 * 1024), 
                SPIFFS.totalBytes() / (1024.0 * 1024),
                (SPIFFS.totalBytes() - SPIFFS.usedBytes()) / (1024.0 * 1024));
        } else {
            Serial.printf("SPIFFS : NON MONTE\n");
        }
    } else {
        Serial.printf("SPIFFS : NON DISPONIBLE\n");
    }
    
    // === FLASH Firmware ===
    uint32_t sketch_size = ESP.getSketchSize();
    uint32_t sketch_partition = (app0) ? app0->size : sketch_size;
    uint32_t sketch_free = sketch_partition - sketch_size;
    Serial.printf("CODE   : %.2f / %.2f Mo (%.2f Mo libre)\n", sketch_size / (1024.0 * 1024), sketch_partition / (1024.0 * 1024), sketch_free / (1024.0 * 1024));
    
    // === FLASH TOTALE ===
    uint32_t flash_total = ESP.getFlashChipSize();
    uint32_t flash_used = sketch_size;
    if (spiffs_partition && spiffs_ready && SPIFFS.begin(true)) {
        flash_used += SPIFFS.usedBytes();
    }
    Serial.printf("TOTAL  : %.2f / %.2f Mo\n", flash_used / (1024.0 * 1024), flash_total / (1024.0 * 1024));
    
    Serial.print("====================\n\n");
}

void modifJson(String type, String key, String newValue, String fileName) {
  if(spiffs_ready == false) {
    DEBUG_PRINTLN("Erreur : SPIFFS n'est pas monté, impossible de modifier le fichier.");
    return;
  }
  // Charger le fichier JSON
  File file = SPIFFS.open(fileName, "r+");
  if (!file) {
      DEBUG_PRINTLN("Erreur d'ouverture du fichier JSON");
      return;
  }

  // Charger et analyser le contenu du fichier JSON
  DynamicJsonDocument doc(1024);
  DeserializationError error = deserializeJson(doc, file);
  if (error) {
      DEBUG_PRINTLN("Erreur de lecture du fichier JSON");
      file.close();
      return;
  }

  // Fermer le fichier après lecture
  file.close();

  // Modifier la valeur en fonction du type
  if (type == "String") {
      doc[key].set(newValue);
  }
  else if (type == "int" || type == "uint8_t" || type == "uint16_t" || type == "uint32_t") {
      // On convertit en int ou en uint (selon le type)
      unsigned long intValue = newValue.toInt();  // Convertir en entier
      if (type == "uint8_t") {
          doc[key].set((uint8_t)intValue);  // On cast en uint8_t
      } else if (type == "uint16_t") {
          doc[key].set((uint16_t)intValue);  // On cast en uint16_t
      } else if (type == "uint32_t") {
          doc[key].set((uint32_t)intValue);  // On cast en uint32_t
      } else {
          doc[key].set(intValue);  // Pour les types int classiques
      }
  }
  else if (type == "bool") {
      doc[key].set(newValue == "true");
  }
  else {
      DEBUG_PRINTLN("Type inconnu");
      return;
  }

  // Sauvegarder les modifications dans le fichier JSON
  file = SPIFFS.open(fileName, "w");
  if (!file) {
      DEBUG_PRINTLN("Erreur d'ouverture du fichier pour écriture");
      return;
  }
  serializeJson(doc, file);
  file.close();
  DEBUG_PRINTLN("Fichier JSON mis à jour : " + String(key) + " = " + String(newValue));
}
String ReadParametreFile() {
  if(spiffs_ready == false) {
    DEBUG_PRINTLN("Erreur : SPIFFS n'est pas monté, impossible de lire le fichier.");
    return "{}";
  }
  File file = SPIFFS.open(PARAMETRE_FILE, "r");
  if (!file) return "{}";  // si le fichier n’existe pas

  StaticJsonDocument<200> doc;
  DeserializationError error = deserializeJson(doc, file);
  file.close();
  if (error) return "{}";

  String jsonResponse;
  serializeJson(doc, jsonResponse);
  return jsonResponse;
}


